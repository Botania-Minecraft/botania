plugins {
    id 'fabric-loom' version '0.9-SNAPSHOT'
    id 'maven-publish'
    id 'com.diffplug.spotless' version '5.8.2'
    id 'io.github.juuxel.loom-quiltflower-mini' version '1.1.0'
    id 'pmd'
}

ext.configFile = file('build.properties')
ext.config = parseConfig(configFile)
ext.priv = parseConfig(file('private.properties'))

def mc_version = "1.17.1"
version = "${mc_version}-${config.build_number}-FABRIC"
if (System.getenv().RELEASE_MODE == null) {
    version += '-SNAPSHOT'
}

group = "vazkii.botania"
archivesBaseName = "${ext.config.mod_name}"

compileJava.options.compilerArgs << "-Xlint:all,-classfile,-processing,-deprecation,-serial"// << "-Werror" TODO reenable when loom mixin AP is fixed

loom {
    accessWidenerPath = file("src/main/resources/botania.accesswidener")
    runs {
        create("GameTest") {
            server()
            vmArg "-Dfabric-api.gametest=1"
        }
    }
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

repositories {
    maven {
        name = "Modmuss"
        url = "https://maven.modmuss50.me/"
    }
    maven {
        name = "Jared"
        url = "https://maven.blamejared.com/"
    }
    maven {
        url = "https://maven.terraformersmc.com/" // for trinkets
    }
    maven {
          name = "Ladysnake Libs"
          url = 'https://ladysnake.jfrog.io/artifactory/mods'
    }
    maven {
        name = "BuildCraft" // for LBA
        url = "https://mod-buildcraft.com/maven"
    }
    maven {
        name "entity reach"
        url "https://maven.jamieswhiteshirt.com/libs-release/"
    }
    maven {
        url "https://jitpack.io" // for step height eam
    }
    maven {
        url "https://maven.shedaniel.me/" // for rei
    }
    maven {
        url "https://server.bbkr.space/artifactory/libs-release/" // for bannerplusplus
    }
    maven {
        name = "ParchmentMC"
        url = "https://maven.parchmentmc.net/"
    }
}

test {
    useJUnitPlatform()
}

dependencies {
    minecraft "com.mojang:minecraft:${mc_version}"
    mappings loom.officialMojangMappings()

    modImplementation "net.fabricmc:fabric-loader:0.11.6"
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.42.1+1.17"

    compileOnly "com.google.code.findbugs:jsr305:3.0.2"

    modImplementation "vazkii.patchouli:Patchouli:1.17.1-58-FABRIC-SNAPSHOT"
    modImplementation "me.shedaniel:RoughlyEnoughItems-fabric:6.0.268-alpha"

    modImplementation "me.zeroeightsix:fiber:0.23.0-2"
    include "me.zeroeightsix:fiber:0.23.0-2"

    // Don't include base and entity as curios includes it already
    modImplementation "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:3.0.0"
    modImplementation "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-entity:3.0.0"

    modImplementation "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-block:3.0.0"
    include "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-block:3.0.0"

    compileOnly "com.demonwav.mcdev:annotations:1.0" // only to shut up compile warnings from CCA's usage of CheckEnv and Env

    modImplementation "dev.emi:trinkets:3.0.4"

    modImplementation "com.jamieswhiteshirt:reach-entity-attributes:2.1.1"
    include "com.jamieswhiteshirt:reach-entity-attributes:2.1.1"

    modImplementation "com.github.emilyploszaj:step-height-entity-attribute:v1.0.1"
    include "com.github.emilyploszaj:step-height-entity-attribute:v1.0.1"

    modImplementation "io.github.fablabsmc:bannerpp:2.0.4+mc.1.17-rc1"
    include "io.github.fablabsmc:bannerpp:2.0.4+mc.1.17-rc1"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.1'
    // PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
    // You may need to force-disable transitiveness on them.
}

spotless {
    enforceCheck = false // workaround around jenkins having wrong line endings in some files
                         // we explicitly check in github actions builds
    java {
        indentWithTabs()
        licenseHeaderFile('spotless/license_template.txt')
        eclipse().configFile 'spotless/eclipseformat.xml'
        removeUnusedImports()
        importOrderFile 'spotless/eclipse.importorder'
    }
}

pmd {
    toolVersion '6.37.0'
    incrementalAnalysis.set(true)
    ruleSets.clear()
    ruleSetFiles = files("spotless/pmd-ruleset.xml")
}

task checkSyntax (group: "verification") {
    dependsOn spotlessJavaCheck, pmdMain
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }

    // Exclude datagenerator .cache directory
    exclude '.cache'

    // Exclude Forge-specific metadata on Fabric only
    exclude 'META-INF/mods.toml'
    exclude 'META-INF/accesstransformer.cfg'

    filesMatching('data/botania/patchouli_books/lexicon/book.json') {
        filter {
            it.replaceAll("buildNumber", config.build_number)
        }
    }
}

task incrementBuildNumber {
    doFirst {
        config.build_number = (config.build_number.toString().toInteger()) + 1
        configFile.withWriter {
            config.toProperties().store(it, "")
        }
    }
}

def parseConfig(File config) {
	if(config.exists()){
		config.withReader {
			def prop = new Properties()
			prop.load(it)
			return (new ConfigSlurper().parse(prop))
		}
	} else {
		return null
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

java {
    withSourcesJar()
}
jar {
    from "LICENSE"

    exclude "**/*.bat"
    exclude "**/*.psd"
    exclude "**/*.exe"
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // select the repositories you want to publish to
    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}

defaultTasks 'clean', 'checkSyntax', 'build'
