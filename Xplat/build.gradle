plugins {
    id('java')
    id('org.spongepowered.gradle.vanilla') version '0.2.1-SNAPSHOT'
}

archivesBaseName = "${mod_name}-xplat"
version = "${minecraft_version}-${build_number}"
if (System.getenv().RELEASE_MODE == null) {
    version += '-SNAPSHOT'
}

minecraft {
    version(minecraft_version)
    accessWideners 'src/main/resources/botania_xplat.accesswidener'
}

repositories {
    maven {
        name = "Jared" // Patchouli and JEI
        url = "https://maven.blamejared.com/"
    }
    maven {
        name = "Unascribed"
        url "https://repo.unascribed.com" // for ears
        content {
            includeGroup "com.unascribed"
        }
    }
    maven {
        url = "https://maven.terraformersmc.com/" // for emi
    }
    maven {
        url "https://maven.shedaniel.me/" // for rei
    }
}

dependencies {
    compileOnly group: 'org.spongepowered', name: 'mixin', version: '0.8.5'
    compileOnly "vazkii.patchouli:Patchouli-xplat:${minecraft_version}-80-SNAPSHOT"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.1'

    // compileOnly 'com.blamejared.crafttweaker:CraftTweaker-common-1.18.2:9.1.123'

    compileOnly "com.unascribed:ears-api:1.4.5"

    compileOnly "mezz.jei:jei-1.20.1-common-api:15.0.0.14"

    // annotationProcessor 'com.blamejared.crafttweaker:Crafttweaker_Annotation_Processors-1.18.2:2.0.0.123'
    // annotationProcessor 'com.blamejared.crafttweaker:CraftTweaker-common-1.18.2:9.1.123'

    compileOnly "dev.emi:emi-xplat-mojmap:1.0.4+1.20.1:api"

    // rei doesn't publish xplat mojmap artifacts, so the forge artifacts will have to suffice
    // it goes without saying, but don't use any of the forge-specific stuff in here
    compileOnly "me.shedaniel:RoughlyEnoughItems-default-plugin-forge:12.0.625"
    // depending on the full jar because api doesn't include the entrypoint annotations
    compileOnly "me.shedaniel:RoughlyEnoughItems-forge:12.0.625"
    compileOnly "me.shedaniel:REIPluginCompatibilities-forge-annotations:12.0.54"
}

test {
    useJUnitPlatform()
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

tasks.withType(JavaCompile).configureEach {
    it.options.compilerArgs.add("-Acrafttweaker.processor.document.output_directory=${new File(rootProject.buildDir, "docsOut")}")
    it.options.compilerArgs.add("-Acrafttweaker.processor.document.multi_source=true")

    it.options.compilerArgs << "-Xmaxerrs" << "2000"

}

processResources {
    // Exclude datagenerator .cache directory
    exclude '.cache'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }
    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}
